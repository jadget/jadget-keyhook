plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'net.researchgate.release' version '2.6.0'
    id "maven-publish"
    id "java"
}

repositories {
    jcenter()
}

dependencies {
    compile 'net.java.dev.jna:jna:5.1.0'
    compile 'net.java.dev.jna:jna-platform:5.1.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/jadget/jadget-keyhook"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'net.jadget'
            artifactId 'jadget-keyhook'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java Keyboard Hooks for Windows')
                root.appendNode('name', 'Jadget-KeyHook')
                root.appendNode('url', 'https://github.com/jadget/jadget-keyhook')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['mavenPublication']

    pkg {
        repo = 'jadget-keyhook'
        name = 'jadget-keyhook'
        userOrg = 'jadget'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jadget/jadget-keyhook'

        version {
            name = project.version
            desc = "Jadget KeyHook Release Build"
            released  = new Date()
        }
    }

    publish = true
}
